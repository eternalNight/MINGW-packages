# Maintainer: Junjie Mao <eternal.n08@gmail.com>

_realname=libart_lgpl
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.3.19
pkgrel=1
pkgdesc='library for high-performance 2D graphics'
groups=("${MINGW_PACKAGE_PREFIX}")
arch=('any')
url='http://www.levien.com/libart'
license=('custom')
source=("http://ftp.gnome.org/pub/gnome/sources/libart_lgpl/2.3/libart_lgpl-2.3.19.tar.bz2"
	"http://www.linuxfromscratch.org/patches/blfs/6.3/libart_lgpl-2.3.19-upstream_fix-1.patch"
	"0001-Fix-an-installation-path-bug.patch")
sha1sums=('7f1ff36c91cdc8f14b62acb50836f52a0c735b9e'
          'b901b64b6ef370ef1eeeb5aa374b91c7fed5534f'
          '1079fcc8b813ce55b445278493fb831a91e347e6')

prepare() {
  cd $srcdir/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/libart_lgpl-2.3.19-upstream_fix-1.patch
  patch -p1 -i ${srcdir}/0001-Fix-an-installation-path-bug.patch
}

build() {
  [ -d "${srcdir}"/build-${CARCH} ] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  ../${_realname}-${pkgver}/configure \
     --prefix=${MINGW_PREFIX} \
     --exec-prefix=${MINGW_PREFIX} \
     --build=${MINGW_CHOST} \
     --host=${MINGW_CHOST} \
     --target=${MINGW_CHOST} \
     --enable-static \
     --enable-shared
  cp ../${_realname}-${pkgver}/libart.def libart.def
  make
}

check() {
  # The package ships with some test tools but provides no automatic check mechanisms
  plain "skip"
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"

  cd "$srcdir"/${_realname}-${pkgver}
  [ -f COPYING ] && install -Dm644 COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
  [ -f AUTHORS ] && install -Dm644 AUTHORS ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/AUTHORS
  [ -f NEWS ] && install -Dm644 NEWS ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/NEWS
}
