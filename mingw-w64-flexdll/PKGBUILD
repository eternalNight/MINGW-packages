# Maintainer: Junjie Mao <eternal.n08@gmail.com>

_realname=flexdll
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.31
pkgrel=1
pkgdesc='An implementation of a dlopen-like API for Windows'
groups=("${MINGW_PACKAGE_PREFIX}")
arch=('any')
url='http://alain.frisch.fr/flexdll.html'
license=('zlib/libpng License')
makedepends=("${MINGW_PACKAGE_PREFIX}-ocaml")
source=("http://alain.frisch.fr/flexdll/${_realname}-${pkgver}.tar.gz"
        "0001-Fix-the-List.hd-exception.patch"
        "0002-Add-path-to-libcrt2.o-into-the-default-library-searc.patch"
        "0003-Remove-the-usages-of-stdout-to-workaround-the-missin.patch")
sha1sums=('7ca63bf8d6c731fd95e0d434a8cfbcc718b99d62'
          '642c99ad0d2035ef028254aff41dbff49014bed3'
          '31ebdca246a4df6069d733350c9a2b8c94d33dec'
          '43f232f04934a0c7797f05e8db03f04ae156d8c4')

env=${MINGW_PREFIX#/}
if [ "${env}" == "mingw32" ]; then
  chain="mingw"
else
  chain="mingw64"
fi
_pkg="${_realname}-${pkgver}"
_pkgsrc="${env}-${_pkg}"
noextract=(${_pkg}.tar.gz)
prepare() {
  cd $startdir/
  [ -d ${srcdir}/${_pkgsrc} ] && rm -rf ${srcdir}/${_pkgsrc}
  tar -xzf ${startdir}/${_pkg}.tar.gz -C ${srcdir}

  cd ${srcdir}
  mv ${_realname} ${_pkgsrc}
  cd ${_pkgsrc}

  patch -p1 -i ${srcdir}/0001-Fix-the-List.hd-exception.patch
  patch -p1 -i ${srcdir}/0002-Add-path-to-libcrt2.o-into-the-default-library-searc.patch
  patch -p1 -i ${srcdir}/0003-Remove-the-usages-of-stdout-to-workaround-the-missin.patch
}

build() {
  cd ${srcdir}/${_pkgsrc}

  export OCAMLLIB=${MINGW_PREFIX}/lib/ocaml
  make flexlink.exe -j1
  make build_${chain} -j1
}

check() {
  # The test demo works on x86_64 but fails on i686. Root causes of the failure
  # is still unknown, but the tool is good enough to be used in other
  # applications such as ocaml and coq.
  plain "skip"
}

package() {
  cd ${srcdir}/${_pkgsrc}

  mkdir -p ${pkgdir}${MINGW_PREFIX}/bin/
  install flexlink.exe ${pkgdir}${MINGW_PREFIX}/bin/
  install flexdll_${chain}.o ${pkgdir}${MINGW_PREFIX}/bin/
  install flexdll_initer_${chain}.o ${pkgdir}${MINGW_PREFIX}/bin/
  for f in flexdll.h flexdll.c flexdll_initer.c default.manifest default_amd64.manifest; do
    install ${f} ${pkgdir}${MINGW_PREFIX}/bin/
  done
  [ -f LICENSE ] && install -Dm644 LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
  [ -f CHANGES ] && install -Dm644 CHANGES ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/CHANGES
}
