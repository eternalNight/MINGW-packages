# Maintainer: Junjie Mao <eternal.n08@gmail.com>

_realname=coq
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=8.4pl5
pkgrel=1
pkgdesc='A formal proof management system'
groups=("${MINGW_PACKAGE_PREFIX}")
arch=('any')
url='https://coq.inria.fr/'
license=('LGPL 2.1')
depends=("${MINGW_PACKAGE_PREFIX}-ocaml"
	 "${MINGW_PACKAGE_PREFIX}-ocaml-camlp4"
	 "${MINGW_PACKAGE_PREFIX}-ocaml-lablgtk")
source=("https://coq.inria.fr/distrib/V8.4pl5/files/coq-8.4pl5.tar.gz"
	"0001-Guess-the-libraries-are-located-at-lib-coq-instead-o.patch")
sha1sums=('107717cbaef3a469e8ff775ae54dbbc457935816'
          '5683480358e92b4eb38adc2402dd0febf37ebb2e')

env=${MINGW_PREFIX#/}
_pkg="${_realname}-${pkgver}"
_pkgsrc="${env}-${_pkg}"
noextract=(${_pkg}.tar.gz)
prepare() {
  cd $startdir/
  [ -d ${srcdir}/${_pkgsrc} ] && rm -rf ${srcdir}/${_pkgsrc}
  tar -xzf ${startdir}/${_pkg}.tar.gz -C ${srcdir}

  cd ${srcdir}
  mv ${_pkg} ${_pkgsrc}
  cd ${_pkgsrc}

  patch -p1 -i ${srcdir}/0001-Guess-the-libraries-are-located-at-lib-coq-instead-o.patch
}

build() {
  cd ${srcdir}/${_pkgsrc}
  export OCAMLLIB=${MINGW_PREFIX}/lib/ocaml
  ./configure -prefix ${MINGW_PREFIX} \
	      -bindir ${MINGW_PREFIX}/bin \
	      -libdir ${MINGW_PREFIX}/lib/coq \
	      -mandir ${MINGW_PREFIX}/share/man \
	      -configdir ${MINGW_PREFIX}/etc/xdg/coq \
	      -datadir ${MINGW_PREFIX}/share/coq \
	      -docdir ${MINGW_PREFIX}/share/doc/coq \
	      -emacslib ${MINGW_PREFIX}/share/emacs/site-lisp/coq \
	      -coqdocdir ${MINGW_PREFIX}/share/texmf/tex \
	      -usecamlp4 -arch win32
  make revision
  make coq
  make coqide -j1
}

check() {
  # Some tests fail due to the following reasons:
  #   * Some signals are not available
  #   * lockf (lock a file region) fails
  # These cases also fail with the official binaries.
  plain "skip"
}

package() {
  cd ${srcdir}/${_pkgsrc}
  export OCAMLLIB=${MINGW_PREFIX}/lib/ocaml
  make install COQINSTALLPREFIX=${pkgdir}/ OLDROOT=$(cygpath -m /)
  [ -f LICENSE ] && install -Dm644 LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
  [ -f COPYRIGHT ] && install -Dm644 LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
  [ -f CREDITS ] && install -Dm644 LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
